apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group 'com.spoqa.stickyscrollview'
version '1.0.1'

ext {
    bintrayRepo = 'maven'
    bintrayName = 'StickyScrollView'
    organization = 'spoqa'

    // Library Details
    publishedGroupId = this.group
    libraryName = 'StickyScrollView'
    artifact = 'StickyScrollView'
    libraryDescription = 'StickyScrollView is a scroll view in which columns are fixed.'
    libraryVersion = this.version

    // Repository Link (For e.g. GitHub repo)
    siteUrl = 'https://github.com/spoqa/StickyScrollView'
    gitUrl = 'https://github.com/spoqa/StickyScrollView.git'
    issueTrackerUrl = 'https://github.com/spoqa/StickyScrollView/issues'
    githubRepository= 'spoqa/StickyScrollView'

    // Developer details
    developerId = 'kimdoori'
    developerName = 'Doori Kim'
    developerEmail = 'dev@spoqa.com'

    // License information
    licenseName = 'The MIT License (MIT)'
    licenseUrl = 'https://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}

group = publishedGroupId

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                groupId publishedGroupId
                artifactId = artifact
                name libraryName
                description = libraryDescription
                url siteUrl

                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

// Avoid Kotlin docs error
tasks.withType(Javadoc) {
    enabled = false
}

// Remove javadoc related tasks
task javadoc(type: Javadoc) {
    // If using .kt files
    excludes = ['**/*.kt']
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_API_KEY")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = issueTrackerUrl
        githubRepo = githubRepository
        licenses = allLicenses
        userOrg = organization
        publish = true
    }
}
